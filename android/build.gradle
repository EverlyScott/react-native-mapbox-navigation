apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

def DEFAULT_COMPILE_SDK_VERSION = 31
def DEFAULT_BUILD_TOOLS_VERSION = '34.0.0'
def DEFAULT_MIN_SDK_VERSION = 21
def DEFAULT_TARGET_SDK_VERSION = 31

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
    ext.kotlin_version = '1.6.0'
    if (project == rootProject) {
        repositories {
            google()
            jcenter()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:8.0.0'
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

android {
    namespace 'com.homee.mapboxnavigation'
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)
    buildToolsVersion safeExtGet('buildToolsVersion', DEFAULT_BUILD_TOOLS_VERSION)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', DEFAULT_MIN_SDK_VERSION)
        targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
    }
}

repositories {
    mavenLocal()
    maven { url "$rootDir/../node_modules/react-native/android" }
    maven { url "$rootDir/../node_modules/jsc-android/dist" }
    google()
    jcenter()
}

dependencies {
    implementation 'com.facebook.react:react-native:+'
    implementation "com.mapbox.navigation:android:2.17.8"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.cardview:cardview:1.0.0'
}

// Define the sourceJar task
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

// Function to configure POM file for Maven
def configurePom(pom) {
    def packageJson = new groovy.json.JsonSlurper().parseText(file('../package.json').text)

    pom.name = packageJson.title
    pom.description = packageJson.description
    pom.url = packageJson.repository.baseUrl
    pom.licenses {
        license {
            name = packageJson.license
            url = packageJson.repository.baseUrl + '/blob/master/' + packageJson.licenseFilename
            distribution = 'repo'
        }
    }
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            groupId 'com.homee.mapboxnavigation'
            artifactId 'mapboxnavigation'
            version '1.0'

            // Specify the artifact to publish
            artifact("$buildDir/outputs/aar/${project.name}-release.aar")

            // Include the source JAR in the publication
            artifact sourceJar

            // Configure the POM file
            pom.withXml {
                def deps = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dep = deps.appendNode('dependency')
                    dep.appendNode('groupId', it.group)
                    dep.appendNode('artifactId', it.name)
                    dep.appendNode('version', it.version)
                }
                configurePom(asNode())
            }
        }
    }

    repositories {
        maven {
            url = uri("${buildDir}/repo")
        }
    }
}